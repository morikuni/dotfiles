
if has('vim_starting')
  set runtimepath+=~/.vim/bundle/neobundle.vim
endif
call neobundle#begin(expand('~/.vim/bundle/'))
	NeoBundleFetch 'Shougo/neobundle.vim'

	"ファイルブラウザ
	NeoBundle 'scrooloose/nerdtree'

	"カラースキーマ
	NeoBundle 'tomasr/molokai'

	"ステータスバーの拡張
	NeoBundle 'itchyny/lightline.vim'

	"インデントを可視化
	NeoBundle 'Yggdroot/indentLine'

	"複数行コメントアウト
	NeoBundle 'tyru/caw.vim'

	"Scalaのシンタックスハイライト
	NeoBundle 'derekwyatt/vim-scala'

	"カーソル移動
	NeoBundle 'Lokaltog/vim-easymotion'

	"Unite
	NeoBundle 'Shougo/unite.vim'
call neobundle#end()

filetype plugin indent on
NeoBundleCheck

"----------
" nerdtree
"----------
" 隠しファイル表示
let NERDTreeShowHidden = 1 
"デフォルトでツリー表示"
let input = expand("%")
if isdirectory(input)
	autocmd VimEnter * execute 'NERDTree '.input
else
	autocmd VimEnter * execute 'NERDTree '.expand("%:h")
endif
"引数なしで実行したとき、NERDTreeを実行
let file_name = expand("%:p")
if has('vim_starting') && file_name == ""
  autocmd VimEnter * execute 'NERDTree ./'
endif

"----------
" molokai
"----------
colorscheme molokai



"----------
" lightline
"----------
let g:lightline = {
			\ 'mode_map': {'c': 'NORMAL'},
			\ 'active': {
			\   'left': [ [ 'mode', 'paste' ], [ 'fugitive', 'relativepath' ] ]
			\ },
			\ 'component_function': {
			\   'modified': 'MyModified',
			\   'readonly': 'MyReadonly',
			\   'fugitive': 'MyFugitive',
			\   'filename': 'MyFilename',
			\   'fileformat': 'MyFileformat',
			\   'filetype': 'MyFiletype',
			\   'fileencoding': 'MyFileencoding',
			\   'mode': 'MyMode'
			\ }
			\ }

function! MyModified()
	return &ft =~ 'help\|vimfiler\|gundo' ? '' : &modified ? '+' : &modifiable ? '' : '-'
endfunction

function! MyReadonly()
	return &ft !~? 'help\|vimfiler\|gundo' && &readonly ? 'x' : ''
endfunction

function! MyFilename()
	return ('' != MyReadonly() ? MyReadonly() . ' ' : '') .
				\ (&ft == 'vimfiler' ? vimfiler#get_status_string() :
				\  &ft == 'unite' ? unite#get_status_string() :
				\  &ft == 'vimshell' ? vimshell#get_status_string() :
				\ '' != expand('%:t') ? expand('%:t') : '[No Name]') .
				\ ('' != MyModified() ? ' ' . MyModified() : '')
endfunction

function! MyFugitive()
	try
		if &ft !~? 'vimfiler\|gundo' && exists('*fugitive#head')
			return fugitive#head()
		endif
	catch
	endtry
	return ''
endfunction

function! MyFileformat()
	return winwidth(0) > 70 ? &fileformat : ''
endfunction

function! MyFiletype()
	return winwidth(0) > 70 ? (strlen(&filetype) ? &filetype : 'no ft') : ''
endfunction

function! MyFileencoding()
	return winwidth(0) > 70 ? (strlen(&fenc) ? &fenc : &enc) : ''
endfunction

function! MyMode()
	return winwidth(0) > 60 ? lightline#mode() : ''
endfunction

"----------
" caw.vim
"----------
nmap ,, <Plug>(caw:i:toggle)


"----------
" EasyMotion
"----------
"デフォルトのショートカットを無効化
let g:EasyMotion_do_mapping = 0
"sキーで検索開始
nmap s <Plug>(easymotion-s2)
vmap s <Plug>(easymotion-s2)
"スマートケースを使用
let g:EasyMotion_smartcase = 1
"アルファベットで日本語を検索できる
let g:EasyMotion_use_migemo = 1


"----------
" unite.vim
"----------
nnoremap <silent> ,b :<C-u>Unite buffer<CR> 
nnoremap <silent> ,r :<C-u>Unite file_mru<CR> 




